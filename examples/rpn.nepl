#entry main
#indent 4
#target wasi

#import "std/mem"
#use std::mem::*
#import "std/math"
#use std::math::*
#import "std/stdio"
#use std::stdio::*

fn main <()*> ()> ():
    // Reverse Polish evaluator for space-separated ASCII lines (REPL).
    // Supported tokens: non-negative integers and + - * operators.
    println "RPN REPL: enter space-separated tokens (e.g. 3 4 +). Ctrl+Z to exit.";
    let mut running <bool> true;
    while running:
        print "> ";
        let input <str> read_line;
        let input_len <i32> load_i32 input;
        if:
            eq input_len 0
            then:
                set running false;
                ()
            else:
                let stack <i32> alloc mul 16 4;
                let mut sp <i32> 0;
                let mut a <i32> 0;
                let mut b <i32> 0;
                let mut i <i32> 0;
                let mut ok <i32> 1;

                while lt i input_len:
                    let mut skip <i32> 0;
                    let ch <i32> load_u8 add input add 4 i;

                    if:
                        eq i 0
                        then:
                            if:
                                eq ch 255
                                then:
                                    set i add i 2;
                                    set skip 1;
                                else:
                                    ()
                            if:
                                eq ch 239
                                then:
                                    set i add i 3;
                                    set skip 1;
                                else:
                                    ()
                        else:
                            ()
                    if:
                        eq skip 1
                        then:
                            ()
                        else:
                            if:
                                le ch 32
                                then:
                                    set i add i 1;
                                    ()
                                else:
                                    if:
                                        lt ch 48
                                        then:
                                            if:
                                                lt sp 2
                                                then:
                                                    println "error: stack underflow";
                                                    set ok 0;
                                                    set i input_len;
                                                else:
                                                    set sp sub sp 1;
                                                    set b load_i32 add stack mul sp 4;
                                                    set sp sub sp 1;
                                                    set a load_i32 add stack mul sp 4;

                                                    if:
                                                        eq ch 43
                                                        then:
                                                            store_i32 add stack mul sp 4 add a b;
                                                            set sp add sp 1;
                                                        else:
                                                            if:
                                                                eq ch 45
                                                                then:
                                                                    store_i32 add stack mul sp 4 sub a b;
                                                                    set sp add sp 1;
                                                                else:
                                                                    if:
                                                                        eq ch 42
                                                                        then:
                                                                            store_i32 add stack mul sp 4 mul a b;
                                                                            set sp add sp 1;
                                                                        else:
                                                                            println "error: unknown operator";
                                                                            set ok 0;
                                                                            set i input_len;
                                                    set i add i 1;
                                        else:
                                            if:
                                                le ch 57
                                                then:
                                                    // Parse a non-negative integer token.
                                                    let mut val <i32> 0;
                                                    let mut parsing <bool> true;
                                                    while parsing:
                                                        if:
                                                            lt i input_len
                                                            then:
                                                                let digit <i32> load_u8 add input add 4 i;
                                                                if:
                                                                    lt digit 48
                                                                    then:
                                                                        set parsing false;
                                                                    else:
                                                                        if:
                                                                            le digit 57
                                                                            then:
                                                                                set val add mul val 10 sub digit 48;
                                                                                set i add i 1;
                                                                            else:
                                                                                set parsing false;
                                                            else:
                                                                set parsing false;
                                                    store_i32 add stack mul sp 4 val;
                                                    set sp add sp 1;
                                                else:
                                                    println "error: unknown token";
                                                    set ok 0;
                                                    set i input_len;

                if:
                    eq ok 1
                    then:
                        if:
                            eq sp 1
                            then:
                                set sp sub sp 1;
                                set a load_i32 add stack mul sp 4;
                                print "rpn result = ";
                                println_i32 a;
                            else:
                                print "error: stack has ";
                                print_i32 sp;
                                println " values";
                    else:
                        ()
    ()
