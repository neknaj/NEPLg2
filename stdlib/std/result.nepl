#indent 4

#import "std/mem" as *

// Generic Result type
enum Result<.T, .E>:
    Ok <.T>
    Err <.E>

fn ok <.T, .E> <(.T)->Result<.T, .E>> (v):
    Result<.T, .E>::Ok v

fn err <.T, .E> <(.E)->Result<.T, .E>> (e):
    Result<.T, .E>::Err e

fn is_ok <.T, .E> <(Result<.T, .E>)->bool> (r):
    match r:
        Result::Ok v:
            true
        Result::Err e:
            false

fn is_err <.T, .E> <(Result<.T, .E>)->bool> (r):
    match r:
        Result::Ok v:
            false
        Result::Err e:
            true

fn unwrap_ok <.T, .E> <(Result<.T, .E>)->.T> (r):
    match r:
        Result::Ok v:
            v
        Result::Err e:
            #intrinsic "unreachable" <> ()

fn unwrap_err <.T, .E> <(Result<.T, .E>)->.E> (r):
    match r:
        Result::Ok v:
            #intrinsic "unreachable" <> ()
        Result::Err e:
            e

fn unwrap_or <.T, .E> <(Result<.T, .E>, .T)->.T> (r, default):
    match r:
        Result::Ok v:
            v
        Result::Err e:
            default

fn map <.T, .U, .E> <(Result<.T, .E>, (.T)->.U)->Result<.U, .E>> (r, f):
    match r:
        Result::Ok v:
            ok<.U, .E> (f v)
        Result::Err e:
            err<.U, .E> e

fn map_err <.T, .E, .F> <(Result<.T, .E>, (.E)->.F)->Result<.T, .F>> (r, f):
    match r:
        Result::Ok v:
            ok<.T, .F> v
        Result::Err e:
            err<.T, .F> (f e)
